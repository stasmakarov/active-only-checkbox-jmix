buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.jmix.gradle:jmix-gradle-plugin:2.7.1"
    }
}

group = 'com.digitilius.jmix'
version = '1.0.0'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url = 'https://global.repo.jmix.io/repository/public'
        }
    }

    jmix {
        bomVersion = '2.7.1'
        projectId = 'aoc'
    }

    group = rootProject.group
    version = rootProject.version

    def archName = project.findProperty('archivesBaseName')?.toString() ?: project.name

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                url = uri("https://maven.pkg.github.com/stasmakarov/active-only-checkbox-jmix")
                credentials {
                    username = project.findProperty('gpr.user')
                            ?: System.getenv('GPR_USER')
                            ?: System.getenv('GITHUB_ACTOR')
                    password = project.findProperty('gpr.token')
                            ?: System.getenv('GPR_TOKEN')
                            ?: System.getenv('GITHUB_TOKEN')
                }
            }
        }

        publications {
            javaMaven(MavenPublication) {
                artifactId = archName
                from components.java

                pom {
                    name = project.name
                    description = 'Jmix add-on: Active-only checkbox'
                    url = 'https://github.com/stasmakarov/active-only-checkbox-jmix'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'stasmakarov'
                            name = 'Stas Makarov'
                            email = 's.makarov@haulmont.com'
                        }
                    }
                }
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-parameters")
    }
}
